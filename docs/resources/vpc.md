---
page_title: "hm_vpc Resource - Hostman"
subcategory: ""
description: |-
  Resource for describing needed VPN and provides actual information about its status
---

# hm_vpc (Resource)

Resource for describing needed VPN and provides actual information about its status

## Usage Examples

```terraform
# Example VPC network
resource "hm_vpc" "example-vpc" {
  name        = "Example VPC"
  description = "Some example VPC"
  subnet_v4   = "192.168.0.0/24"
  location    = "us-2"
}


### Example servers with VPC network

data "hm_presets" "example-preset" {
  price_filter {
    from = 30
    to   = 40
  }
}

data "hm_os" "example-os" {
  name    = "ubuntu"
  version = "22.04"
}

# With VPC network
resource "hm_server" "example-server-with-local-network" {
  name  = "Example server with local network"
  os_id = data.hm_os.example-os.id

  preset_id = data.hm_presets.example-preset.id

  local_network {
    id = hm_vpc.example-vpc.id
  }
}

# With VPC network and specified ID IP
resource "hm_server" "example-server-with-local-network-and-address" {
  name  = "Example server with local network and address"
  os_id = data.hm_os.example-os.id

  preset_id = data.hm_presets.example-preset.id

  local_network {
    id = hm_vpc.example-vpc.id
    ip = "192.168.0.15"
  }
}

### Example database with VPC network
data "hm_database_preset" "example-db-preset-redis-7" {
  location = "us-2"

  type = "redis"

  disk = 8 * 1024

  price_filter {
    from = 10
    to   = 50
  }
}

resource "hm_database_cluster" "example-redis-7" {
  name = "example_redis7"

  type = "redis7"

  preset_id = data.hm_database_preset.example-db-preset-redis-7.id

  network {
    id = hm_vpc.example-vpc.id
  }
}

### Example load balancer with VPC network

data "hm_lb_preset" "example-lb-preset" {
  requests_per_second = "10K"

  price_filter {
    from = 10
    to   = 20
  }
}

resource "hm_lb" "example-lb" {
  name = "example-lb"

  algo = "roundrobin"

  is_sticky    = false
  is_use_proxy = false
  is_ssl       = false
  is_keepalive = false

  health_check {
    proto = "http"

    port = 80

    path = "/lala"

    inter   = 10
    timeout = 5
    fall    = 3
    rise    = 2
  }

  ips = []

  preset_id = data.hm_lb_preset.example-lb-preset.id

  project_id = resource.hm_project.example-project.id

  local_network {
    id = hm_vpc.example-vpc.id
    ip = "192.168.0.20"
  }
}
```
<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `location` (String) Location for VPC
- `name` (String) Name for VPC
- `subnet_v4` (String) Subnet for VPC in CIDR notation

### Optional

- `description` (String) Description for VPC

### Read-Only

- `id` (String) The ID of this resource.

